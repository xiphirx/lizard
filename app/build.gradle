apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion versions.compileSdk
  buildToolsVersion versions.buildTools

  defaultConfig {
    applicationId "gg.destiny.lizard"
    minSdkVersion versions.minSdk
    targetSdkVersion versions.targetSdk
    versionCode versions.versionCode
    versionName versions.versionName
    multiDexEnabled = true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
  }

  applicationVariants.all { variant ->
    variant.buildConfigField "String", "TWITCH_CLIENT_ID", "\"$TWITCH_CLIENT_ID\""
    variant.buildConfigField "String", "TWITCH_STREAM_ID", "\"$TWITCH_STREAM_ID\""

    def mode = variant.buildType.name.toLowerCase().equals("debug") ? "debug" : "release"
    def adbExe = android.getAdbExe().toString()
    ["emu", "dev"].each { device ->
      def isEmulator = device == "emu"
      def adbTarget = isEmulator ? "-e" : "-d"
      def version = "app-${mode}.apk"
      def apk = variant.outputs.find { it.outputFile.name.toLowerCase().endsWith(version) }

      if (apk == null) return

      task "install$mode$device"(dependsOn: "assemble${mode.capitalize()}", type: Exec) {
        commandLine adbExe, adbTarget, "install", "-r", apk.outputFile
      }
      task "run$mode$device"(dependsOn: "install$mode$device", type: Exec) {
        commandLine adbExe, adbTarget, "shell", "monkey", "-p", variant.applicationId, "-c", "android.intent.category.LAUNCHER", "1"
      }
    }
  }
}

dependencies {
  implementation project(':core')

  implementation deps.appCompat
  implementation deps.design
  implementation deps.cardView
  implementation deps.annotations
  implementation deps.kotlinStdlib
  implementation deps.timberkt
  implementation deps.glide
  implementation deps.flexadapter
  implementation deps.jodatime
  implementation deps.mosby

  implementation deps.dagger
  kapt deps.daggerCompiler

  implementation deps.conductor
  implementation deps.conductorSupport

  implementation deps.moshi
  implementation deps.moshiKotlin

  implementation deps.okhttp
  implementation deps.okhttpLogInterceptor

  implementation deps.retrofit
  implementation deps.retrofitRxAdapter
  implementation deps.retrofitScalarsConverter
  implementation deps.retrofitMoshiConverter

  implementation deps.rxJava
  implementation deps.rxAndroid
  implementation deps.rxRelay
  implementation deps.rxBinding
}

repositories {
  mavenCentral()
}
